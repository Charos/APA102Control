apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "ch.bfh.ti.apa102control"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        ndk {
            moduleName "spi"
        }
    }
    //Point Gradle at the director where you want to root all of your native code
    sourceSets.main{
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs=[]//Disable automatic ndk-build call
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile project(':colorseekbar')
}

import org.apache.tools.ant.taskdefs.condition.Os
def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null)
    return System.env.ANDROID_NDK_ROOT
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")
    return ndkdir
    }
//call regular ndk-build(.cmd) script for app directory
task ndkBuild(type:Exec){
    if(Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'ndk-build.cmd', '-C', file('src/main').absolutePath
    }else{
        def ndkDir=getNdkDir();
        commandLine "$ndkDir/ndk-build",'-C',file('src/main').absolutePath
    }
}
tasks.withType(JavaCompile){
    compileTask->compileTask.dependsOn ndkBuild
}
